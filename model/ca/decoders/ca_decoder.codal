/**
 * Codasip s.r.o.
 *
 * CONFIDENTIAL
 *
 * Copyright 2022 Codasip s.r.o.
 *
 * All Rights Reserved.
 *
 * NOTICE: All information contained in this file, is and shall remain the property of
 * Codasip s.r.o. and its suppliers, if any.
 *
 * The intellectual and technical concepts contained herein are confidential and proprietary to
 * Codasip s.r.o. and are protected by trade secret and copyright law.  In addition, elements of the
 * technical concepts may be patent pending.
 *
 * This file is part of the Codasip Studio product. No part of the Studio product, including this
 * file, may be use, copied, modified, or distributed except in accordance with the terms contained
 * in Codasip license agreement under which you obtained this file.
 *
 *  \file   ca_decoder.codal
 *  \author Codasip
 *  \date   09.02.2022
 *  \brief  cycle accurate model
 */

#include "ca_defines.hcodal"
#include "opcodes.hcodal"
#include "config.hcodal"
#include "common_enums.hcodal"


// -------------------------------------------------------------------------------------------------
// Decoder
// -------------------------------------------------------------------------------------------------

set dec = i_hw_halt
                ,i_hw_rtype_alu
                ,i_hw_itype_alu
                ,i_hw_itype_special
                ,i_hw_btype_branches
                ,i_hw_utype_ops
                ,i_hw_itype_jlreg
                ,i_hw_itype_jlink
                ;

// -------------------------------------------------------------------------------------------------
// Halt instruction
// -------------------------------------------------------------------------------------------------

// This instruction has no operands.

element i_hw_halt
{
    use opc_halt as opc;

    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable
        s_id_regwrite = WE_FALSE;

        // Operations
        s_id_aluop = ALU_NOP;

        // Operand MUXes
        s_id_alusrc2 = DONT_CARE;
        s_id_alusrc1 = DONT_CARE;

        // HALT Command
        s_id_halt = HALT_HALT;

        // BRANCH command
        s_id_brnchop = BRANCH_FALSE;

        // Immediate value
        s_id_immsel = IMM_NONE;

        // Multiplexor select
        s_id_rfwt_sel = DONT_CARE;

        // Branch selection
        s_id_brnch_sel = DONT_CARE;
    };
};

// -------------------------------------------------------------------------------------------------
// Arithmetic instructions
// -------------------------------------------------------------------------------------------------

element i_hw_rtype_alu
{
    use opc_rtype_alu as opc;

    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable
        s_id_regwrite = WE_TRUE;

        switch (opc)
        {
            case RTYPE_ADD:
                s_id_aluop = ALU_ADD;
                break;
            case RTYPE_SUB:
                s_id_aluop = ALU_SUB;
                break;
            case RTYPE_AND:
                s_id_aluop = ALU_AND;
                break;
            case RTYPE_OR:
                s_id_aluop = ALU_OR;
                break;
            case RTYPE_XOR:
                s_id_aluop = ALU_XOR;
                break;
            case RTYPE_SLL:
                s_id_aluop = ALU_SLL;
                break;
            case RTYPE_SRL:
                s_id_aluop = ALU_SRL;
                break;
            case RTYPE_SRA:
                s_id_aluop = ALU_SRA;
                break;
            case RTYPE_SLT:
                s_id_aluop = ALU_SLT;
                break;
            case RTYPE_SLTU:
                s_id_aluop = ALU_SLTU;
                break;

            default:
                codasip_fatal(ALU_ERROR, "Default case: %d\n", opc);
                break;
        }

        // Operand MUXes
        s_id_alusrc2 = ALU2_REG;
        s_id_alusrc1 = ALU_SRC1_REG;

        // HALT Command
        s_id_halt = HALT_NOHALT;

        // BRANCH Command
        s_id_brnchop = BRANCH_FALSE;

        // Immediate Value
        s_id_immsel = IMM_NONE;

        // Multiplexor select  
        s_id_rfwt_sel = RFWT_ALU;

        // Branch selection
        s_id_brnch_sel = BRADD_ADDR;
    };
};

element i_hw_itype_alu
{
    use opc_itype_alu as opc;

    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable
        s_id_regwrite = WE_TRUE;

        switch (opc)
        {
            case ITYPE_ADDI:
                s_id_aluop = ALU_ADD;
                break;
            case ITYPE_ANDI:
                s_id_aluop = ALU_AND;
                break;
            case ITYPE_ORI:
                s_id_aluop = ALU_OR;
                break;
            case ITYPE_XORI:
                s_id_aluop = ALU_XOR;
                break;
            case ITYPE_SLTI:
                s_id_aluop = ALU_SLT;
                break;
            case ITYPE_SLTIU:
                s_id_aluop = ALU_SLTU;
                break;

            default:
                codasip_fatal(ALU_ERROR, "Default case: %d\n", opc);
                break;
        }

        // Operand MUXes
        s_id_alusrc2 = ALU2_IMM;
        s_id_alusrc1 = ALU_SRC1_ZERO;

        // HALT Command
        s_id_halt = HALT_NOHALT;

        // BRANCH Command
        s_id_brnchop = BRANCH_FALSE;

        // IMM value
        s_id_immsel = IMM_ITYPE;

        // Multiplexor select
        s_id_rfwt_sel = RFWT_ALU;

        // Branch selection
        s_id_brnch_sel = BRADD_ADDR;
    };
};

element i_hw_itype_special
{
    use opc_itype_special as opc;

    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable
        s_id_regwrite = WE_TRUE;

        switch (opc)
        {
            case IMM_SHIFT_SLLI:
                s_id_aluop = ALU_SLL;
                break;
            case IMM_SHIFT_SRAI:
                s_id_aluop = ALU_SRA;
                break;
            case IMM_SHIFT_SRLI:
                s_id_aluop = ALU_SRL;
                break;

            default:
                codasip_fatal(ALU_ERROR, "Default case: %d\n", opc);
                break;
        }

        // Operand MUXes
        s_id_alusrc2 = ALU2_IMM;
        s_id_alusrc1 = ALU_SRC1_REG;

        // HALT Command
        s_id_halt = HALT_NOHALT;

        // BRANCH Command
        s_id_brnchop = BRANCH_FALSE;

        // IMM value
        s_id_immsel = IMM_ITYPE;

        // Multiplexor select
        s_id_rfwt_sel = RFWT_ALU;

        // Branch selection
        s_id_brnch_sel = BRADD_ADDR;
    };
};

element i_hw_btype_branches
{
    use opc_btype_branches as opc;

    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable
        s_id_regwrite = WE_FALSE;

        switch (opc)
        {
            case BTYPE_BEQ:
                s_id_aluop = ALU_SUB;
                s_id_brnchop = BRANCH_COND_TRUE;
                break;
            case BTYPE_BNE:
                s_id_aluop = ALU_SUB;
                s_id_brnchop = BRANCH_COND_FALSE;
                break;
            case BTYPE_BLT:
                s_id_aluop = ALU_SLT;
                s_id_brnchop = BRANCH_COND_FALSE;
                break;
            case BTYPE_BGE:
                s_id_aluop = ALU_SLT;
                s_id_brnchop = BRANCH_COND_TRUE;
                break;
            case BTYPE_BLTU:
                s_id_aluop = ALU_SLTU;
                s_id_brnchop = BRANCH_COND_FALSE;
                break;
            case BTYPE_BGEU:
                s_id_aluop = ALU_SLTU;
                s_id_brnchop = BRANCH_COND_TRUE;
                break;

            default:
                codasip_fatal(ALU_ERROR, "Default case: %d\n", opc);
                break;
        }

        // Operand MUXes
        s_id_alusrc2 = ALU2_REG;
        s_id_alusrc1 = ALU_SRC1_REG;

        // HALT Command
        s_id_halt = HALT_NOHALT;

        // Immediate Value
        s_id_immsel = IMM_BTYPE;

        // Multiplexor select  
        s_id_rfwt_sel = RFWT_ALU;

        // Branch selection
        s_id_brnch_sel = BRADD_ADDR;
    };
};

element i_hw_utype_ops
{
    use opc_utype_ops as opc;
    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable

        s_id_regwrite = WE_TRUE;
        s_id_rfwt_sel = RFWT_ALU;

        // ALU Operation
        s_id_aluop = ALU_ADD;

        // Operand MUXes
        switch (opc)
        {
            case UTYPE_LUI:
                s_id_alusrc1 = ALU_SRC1_ZERO;
                break;
            case UTYPE_AUIPC:
                s_id_alusrc1 = ALU_SRC1_PC;
                break;
        }
        s_id_alusrc2 = ALU2_IMM;
        s_id_immsel = IMM_UTYPE;

        // Branch control
        s_id_brnchop = BRANCH_FALSE;
        s_id_brnch_sel = BRADD_ADDR;

        // HALT Command
        s_id_halt = HALT_NOHALT;

    };

};

element i_hw_itype_jlreg
{

    use opc_itype_jlreg as opc;
    assembly { opc };
    binary { opc };

    semantics
    {
        // Register file write enable
        s_id_regwrite = WE_TRUE;
        s_id_rfwt_sel = RFWT_PC;

        // ALU Operation
        s_id_aluop = ALU_ADD;

        // Operand MUXes
        s_id_alusrc1 = ALU_SRC1_REG;
        s_id_alusrc2 = ALU2_IMM;
        s_id_immsel = IMM_ITYPE;

        // Branch control
        s_id_brnchop = BRANCH_TRUE;
        s_id_brnch_sel = BRADD_ALU;

        // HALT Command
        s_id_halt = HALT_NOHALT;

    };

};

 

// -------------------------------------------------------------------------------------------------

// JAL instruction

// -------------------------------------------------------------------------------------------------

 

element i_hw_itype_jlink

{

    use opc_jtype_jlink as opc;

 

    assembly { opc };

    binary { opc };

 

    semantics

    {

        // Register file write enable

        s_id_regwrite = WE_TRUE;

        s_id_rfwt_sel = RFWT_PC;

 

        // ALU Operation

        s_id_aluop = ALU_NOP;

 

        s_id_alusrc1 = ALU_SRC1_PC;

        s_id_alusrc2 = ALU2_IMM;

        s_id_immsel = IMM_JTYPE;

 

        // Branch control

        s_id_brnchop = BRANCH_TRUE;

        s_id_brnch_sel = BRADD_ADDR;
 

        // HALT Command

        s_id_halt = HALT_NOHALT;

    };

};